plugins {
    alias(libs.plugins.kotlinJvm)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.ktor)
    alias(libs.plugins.sqldelight)
    application
}

group = "se.soderbjorn.termtastic"
version = "1.0.0"

val webDistDir = project(":web").layout.buildDirectory.dir("dist/js/productionExecutable")
val embeddedWebResourcesDir = layout.buildDirectory.dir("generated/web-resources")

application {
    mainClass.set("se.soderbjorn.termtastic.ApplicationKt")

    val isDevelopment: Boolean = project.ext.has("development")
    applicationDefaultJvmArgs = listOf(
        "-Dio.ktor.development=$isDevelopment",
        "-Dtermtastic.webDist=${webDistDir.get().asFile.absolutePath}"
    )
}

// Stage the web bundle under build/generated/web-resources/web/ so it ends up
// inside the shadow jar at /web on the classpath. The packaged server reads it
// via staticResources when no on-disk webDist is provided.
val copyWebDistToResources by tasks.registering(Copy::class) {
    dependsOn(":web:jsBrowserDistribution")
    from(webDistDir)
    into(embeddedWebResourcesDir.map { it.dir("web") })
}

sourceSets["main"].resources.srcDir(embeddedWebResourcesDir)

tasks.named("processResources") {
    dependsOn(copyWebDistToResources)
}

dependencies {
    implementation(projects.clientServer)
    implementation(libs.logback)
    implementation(libs.ktor.serverCore)
    implementation(libs.ktor.serverNetty)
    implementation(libs.ktor.serverWebsockets)
    implementation(libs.ktor.serverContentNegotiation)
    implementation(libs.ktor.serializationKotlinxJson)
    implementation(libs.ktor.networkTlsCertificates)
    implementation(libs.kotlinx.coroutines.core)
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.pty4j)
    implementation(libs.jediterm.core)
    implementation(libs.sqldelight.sqliteDriver)
    implementation(libs.sqldelight.coroutinesExtensions)
    implementation(libs.flexmark.all)
    implementation(libs.jsoup)
    implementation(libs.compose.runtime)
    implementation(libs.compose.foundation)
    implementation(libs.compose.material3)
    implementation(libs.compose.ui)
    implementation(compose.desktop.currentOs)
    testImplementation(libs.ktor.serverTestHost)
    testImplementation(libs.kotlin.testJunit)
}

sqldelight {
    databases {
        create("TermtasticDatabase") {
            packageName.set("se.soderbjorn.termtastic.db")
        }
    }
}

// Ensure the web bundle exists before the server starts.
// Dev server runs on a non-production port so a packaged Termtastic on
// SERVER_PORT (8082) can keep running alongside developer iterations.
// It also writes to its own SQLite database (`termtastic-dev.db` next to
// the production one) so a packaged build using stale code can't stomp on
// the dev server's window config — both processes used to share the same
// `termtastic.db` and the older one would silently strip fields it didn't
// recognise on its next save, e.g. losing freshly-introduced LeafContent
// variants like the markdown overview pane.
tasks.named<JavaExec>("run") {
    dependsOn(":web:jsBrowserDistribution")
    systemProperty("termtastic.port", "8083")
    val devDb = File(
        System.getProperty("user.home"),
        "Library/Application Support/Termtastic/termtastic-dev.db",
    )
    systemProperty("termtastic.dbPath", devDb.absolutePath)
}
